@echo off
if #%1==# (goto usage)
setlocal

iff not "%MSETUP"=="" then
  call chkmsetup
  gosub #local_extract
endiff



call mtex-env -clear
set /r "%MTEX\_\utils.cfg"
iff exist "%ETC\utils.cfg" then
  set /r "%ETC\utils.cfg"
endiff
set _d=%@if[#%2==#,%@name[%1],%2]
set _b=%@if[%@index[/%Editors/,/%_d/]==-1,utils,editors]
set _r=%TMP
msg 0 I cannot find util [%1] in folder [%UTILS\%_d] or local utils folder! Not installed? Please choose: //Search PATH/Specify a folder/Install from RAR archieve/Download and install/Cancel~~
msg 936 没有在[%UTILS\%_d]或本地工具目录发现指定的工具[%1]，您可能没有安装它。请选择：//搜索PATH路径/指定路径/从压缩包安装/在线下载并安装/取消~~
wask {130}%_msg
set _i=%?
switch %_i
case -1 .OR. 5
  set _a=%@execstr[call %MTEX\bin\search.btm %1]
case 1
  apath %UTILS\%_d
  if isdir "%UTILSLOCAL" (apath %UTILSLOCAL\%_d)
  set _a=%@execstr[call %MTEX\bin\search.btm %1]
case 2
  set _a=%@getfile[c:\,*.exe;*.com;*.cmd;*.bat;*.btm]
  if not #%_a==# gosub #save
case 3
  set _u=%@getfile[%_r,%_d.rar]
  iff exist "%_u" then 
    gosub #install_util 
    apath %UTILS\%_d
	apath %UTILSLOCAL\%_d
    apath %MTEX\%_d
    set _a=%@execstr[call %MTEX\bin\search.btm %1 %UTILS\%_d;%UTILSLOCAL\%_d]  
  endiff
case 4
  gosub #download_util
  set _u=%_r\%_d.rar
  iff exist "%_u" then 
    gosub #install_util 
    apath %UTILS\%_d
	apath %UTILSLOCAL\%_d
    apath %MTEX\%_d
    set _a=%@execstr[call %MTEX\bin\search.btm %1 %UTILS\%_d;%UTILSLOCAL\%_d]  
  endiff
default
  msgbox OK Wrong choice? I will quit now ...
  quit
endswitch
if #%_a==# (goto #error)
gosub #save_path
quit


:#error
msg 0 Still not found! Please check PATH variable or folder [%UTILS] to see whether this util is installed.~~
msg 936 仍然找不到，请检查PATH环境变量。~~
MsgBox OK %_msg
cancel 0
quit

:#save_path
del /z /q "%ETC\add_util.sav" >& nul
iff not %@index[%_a,%MTEX\bin\util]==0 then
  echo %_a > "%ETC\add_util.sav"
  echo Detecting path of util file: %_a
endiff
return

:#save
msg 0 Save setting [%_d=%_a]? If you choose YES, I will not ask you again when running util [%_d] next time.~~
msg 936 是否保存设置[%_d=%_a]？保存后下一次运行工具[%_d]将不再提问路径。~~
msgbox YESNOCANCEL %_msg
set _ask=%_?
iff %_?==%_ask then
  quit
elseiff %_ask==10 then
  set _save=%@iniwrite[%ETC\mtex.ini,Utils,%_d,%_a]
  if %_save==0  (echo Config of util [%_d] written in %ETC\mtex.ini !) 
  call mtex-env -clear
endiff
return


:#download_util
if not #%_i==#4 (return)
:download
set server=%@line["%MTEX\_\mserver.cfg",0]
iff exist "%ETC\mserver.cfg" then
  set server=%@line["%ETC\mserver.cfg",0]
endiff
set server=%@execstr[call ask_server.btm]

iff exist "%_r\%_d.rar" then
  msg 0 Do you really want to overwrite file [%_r\%_d.rar]?~~
  msg 936 文件[%_r\%_d.rar]已存在，是否确认要覆盖它？~~
  msgbox YESNOCANCEL %_msg
  if %_?==12 return
endiff
set url=%[server]%[_b]/%_d.rar
msg 0 Ready to download util [%_d] from server [%server], please confirm the URL and then wait until downloaded.~~
msg 936 即将下载如下文件，请确认网址。下载文件可能较慢，确认后请耐心等待。~~
querybox /E %_msg %%url
if %_?==2 (return)
window RESTORE
echo Downloading file [%url] to [%_r\%_d.rar], please wait until finished ...

pushd "%_r"\

echo Running: down_url "%_d.rar" "%url"
down_url "%_d.rar" "%url"

popd
echo Downloading finished? I will test the rar archieve:-)

unrar t "%_r\%_d.rar"
iff %?==0 then 
  msg 0  Downloading finished! File [%_d.rar] is saved in folder [%_r]!~~
  msg 936 下载完毕！文件[%_d.rar]保存于文件夹[%_r]！~~
  msgbox OK %_msg
else
  del "%_r\%_d.rar"
  msg 0 Error in downloading? Please make sure the URL is correct! Try another server?~~
  msg 936 下载似乎有错误！请确认下载地址正确！是否尝试其它服务器？~~
  msgbox OKCANCEL %_msg
  if %_?==10 (goto download)
endiff

return

:#install_util 
echo Installing util [%_d] from file [%_u] ...
set _w=%@if[isdir "%UTILSLOCAL",%UTILSLOCAL,%UTILS]
iff "%_d"=="winedt" then
  set _w=%MTEX
endiff
unrar x -o+ %@sfn[%_u] "%_w"\
iff not isdir "%_w"\"%_d"  then
  echoerr Folder "%_w"\"%_d" does not exist!
  return
endiff
pushd "%_w\%_d"

iff exist "_\u-*.btm" then
    pushd 
    setlocal
    call "%@findfirst[_\u-*.btm]"
    endlocal
    popd
endiff

iff exist "_\download.ini"  then
  echo Warning: I need to download the installer or original archive first ...
  call down_src.btm "_\download.ini"
endiff

pushd
setlocal
if exist "_\c-*.btm"  call "%@findfirst[_\c-*.btm]"
if exist "_\L-*.btm"  call "%@findfirst[_\L-*.btm]"
endlocal
popd

popd
return

:#local_extract
set _u=%1
if #%@ext[%1]==# set _u=%1.rar
iff not #%@ext[%_u]==#rar then
  echoerr Currently I can only support RAR archive!
  return
endiff
iff #%@path[%1]==# then
  set _u=%@execstr[call %MTEX\bin\search.btm %_u %MSETUP]
  if #%_u==#  return
endiff
echoerr Automatically installing util archive [%_u] ...
set _d=%@if[#%2==#,%@name[%1],%2]
iff exist "%_u" then 
  gosub #install_util 
  set _a=%@execstr[call %MTEX\bin\search.btm %1 %UTILS\%_d;%UTILSLOCAL\%_d;%MTEX\%_d]  
  gosub #save_path
endiff
quit

:usage
echo Usage1: %0 utilname ==) Add/install specified util
echo Usage2: %0 exefile [utilname] ==) Add/install specified util and detect the exefile path
echo Usage3: %0 rarfile [utilname] ==) Add/install specified util from rarfile 
echo Note: If you set 
quit