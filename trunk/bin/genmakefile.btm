@echo off
setlocal
pushd %@path[%1]

set /r "%MTEX\_\openx.cfg" >& nul
iff exist "%ETC\openx.cfg" then
  set /r "%ETC\openx.cfg" >& nul
endiff

iff exist Makefile then
  copy Makefile Makefile.old
endiff

echo #Makefile generated by "Gen-Makefile" of openx.btm >Makefile
iff defined MakefileHead then
  type %[MakefileHead] >>Makefile
endiff

set _tmpmak="%TMP\_genmak_.tmp"
del %_tmpmak>& nul

iff defined APPTYPES then
  set appmenu=%APPTYPES
else
  set appmenu=Default .EXE/Library .DLL/Document .PDF/Document .PS/Document .HTM
endiff
iff #%1==# then
  set APP=main
else
  set APP=%@name[%1]
endiff
set APPTYPE=%@execstr[wask.exe {140}Please choose the application or document type//%appmenu]
set APPEXT=%@ext[%APPTYPE]
iff not #%APPEXT==# then
  set APP=%APP.%APPEXT
endiff

querybox /E Please confirm application or document name here: %%APP

set _LD=%@word[": ",0,%APPTYPE]
iff not defined Link_%[_LD] then
  iff %@index[%APPTYPE,Document]==-1 then
    set _LD=MingwGCC
    iff defined Linkers then
      set _LD=%@execstr[wask.exe {140}Please choose a linker//%Linkers]
    endiff
  endiff
endiff
echo Link_%[_LD]=%[!Link_%_LD] >>Makefile

set OBJ=%@if[%@wild[%_LD,Mingw*]==1,o,obj]
echo OBJ=%OBJ >>Makefile


iff #%SRC_EXTS==# then
  set SRC_EXTS=bpr dpr cpp c cxx pas pp asm rc f f90 for def h hpp
endiff
querybox /E Please input source extensions here: %%SRC_EXTS

set SRC_FILES=
for %e in (%SRC_EXTS) do (
  gosub #proc-ext
)
echo.>>%_tmpmak
querybox /E Please confirm source files here: %%SRC_FILES


set obj_files=
set progs=%APP
for %z in (%[SRC_FILES]) do gosub #proc-file
REM ~ querybox /E Please confirm object files here: %%obj_files


echo src_files=%[SRC_FILES] >>Makefile
echo obj_files=%[obj_files] >>Makefile
echo lib_files=%[lib_files] >>Makefile
echo APP=%APP >>Makefile

echo.>>Makefile
echo CFLAGS=%[CFLAGS] >>Makefile
echo CPPFLAGS=%[CPPFLAGS] >>Makefile
echo CXXFLAGS=%[CXXFLAGS] >>Makefile
echo AFLAGS=%[AFLAGS] >>Makefile
echo RFLAGS=%[RFLAGS] >>Makefile
echo LDFLAGS=%[LDFLAGS] >>Makefile

echo.>>Makefile
echo COMSPEC=%COMSPEC >>Makefile
echo RM=$(COMSPEC) /c del >>Makefile
echo # CC=gcc >>Makefile
echo # CXX=gcc >>Makefile
echo # CXX=gcc >>Makefile

echo.>>Makefile
echo .PHONY : all clean >>Makefile
echo all: %progs >>Makefile

iff defined !Requisite_%APPEXT then
  set APPREQ=%[!Requisite_%APPEXT]
else
  set APPREQ=$(obj_files) $(res_files)
endiff

echo %[APP] : %[APPREQ] >>Makefile
iff defined _LD then
  echo %@char[9] $(Link_%[_LD]) >>Makefile
endiff

echo.>>Makefile
type %_tmpmak >>Makefile
echo.>>Makefile
echo clean: >>Makefile
iff #%CLEAN_FILES==# then
  set CLEAN_FILES=$(obj_files) $(res_files)
endiff
echo %@char[9] $(RM) %progs %[CLEAN_FILES] >>Makefile

iff defined MakefileTail then
  type %[MakefileTail] >>Makefile
endiff

del %_tmpmak %_tmplst >& nul

start tview.exe "%_CWD\Makefile"

popd
quit


:#proc-ext
if not exist *.%e return
set _files=%@expand[*.%e]
set %e_files=%_files
echo %e_files=%_files >>Makefile
iff #%e==#rc then
  echo res_files=%@replace[.rc,.res,%_files] >>Makefile
endiff
if not defined !Compile_%e return
::if not defined !Object.%e return
set _e=%[!Object_%e]
if #%_e==# (set _e=$(OBJ))
set _c=%[!Compile_%e]  
echo .%e.%[_e]:>>%_tmpmak 
echo %@char[9] %_c `$<`>>%_tmpmak
set SRC_FILES=%[SRC_FILES] %_files

set _e2=%[!Object_%_e]
set _c2=%[!Compile_%_e] 
iff defined _e2 .AND. defined _c2 then
	echo .%[_e].%[_e2]:>>%_tmpmak 
    iff %@index[%[_c2],$]==-1 then
      echo %@char[9] %[_c2] `$<`>>%_tmpmak
    else
      echo %@char[9] %[_c2] >>%_tmpmak
    endiff
endiff
return


:#proc-file
  echo %z
  set e=%@ext[%z]
  if #%e==#  return
  set n=%@name[%z]
  set _e=%[!Object_%e]
  if #%_e==# (set _e=$(OBJ))
  iff #%_e==#exe then
    set progs=%progs %[n].%[_e]
  elseiff #%_e==#obj then
    set obj_files=%[obj_files] %[n].%[_e]
  elseiff #%_e==#o then
    set obj_files=%[obj_files] %[n].%[_e]
  elseiff "%_e"=="$(OBJ)" then
	set obj_files=%[obj_files]  %[n].%[_e]
  endiff
  set _h=%@if[%e==c,$(h_files),%@if[%e==cpp,$(hpp_files),]]
  echo %[n].%[_e] : %n.%e %_h >>%_tmpmak 
return